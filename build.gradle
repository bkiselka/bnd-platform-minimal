buildscript {
	repositories {
		mavenCentral()
	}
	dependencies {
		classpath 'org.standardout:bnd-platform:1.2.0'
		// uncomment line below if using Gradle 1.x
		// classpath 'org.codehaus.groovy:groovy-backports-compat23:2.3+'
	}
}

apply plugin: 'org.standardout.bnd-platform'

/* XXX Problem with new plugin notation: dependency of bnd-platform is not resolved correctly / cannot be loaded as plugin
plugins {
  id "org.standardout.bnd-platform" version "1.2.0"
}
*/

repositories {
	mavenCentral()
}

defaultTasks 'clean', 'updateSiteZip'

platform {
	fetchSources = true
	determineImportVersions = true

	override {
		optionalImport(
			// no version requirement for certain JRE provided packages
			// use wildcards to not enforce imports
			'javax.xml.*',
			// also make JUnit optional everywhere - so we can exclude it from products
			'junit.framework.*',
			'org.junit.*'
		)
	}

	def poiVersion = '3.11'
	feature id: 'platform.shared.poi',
			name: 'Apache POI',
			version: poiVersion, {
		// merged POI bundle
		merge(failOnDuplicate: false) {
			bundle "org.apache.poi:poi:$poiVersion"
			bundle "org.apache.poi:poi-ooxml:$poiVersion"
			bundle "org.apache.poi:poi-ooxml-schemas:$poiVersion"

			bnd {
				symbolicName = 'org.apache.poi'
				bundleName = 'Apache POI'
				version = poiVersion
				// only provide poi packages as exported packages
				instruction 'Export-Package', "org.apache.poi.*;version=$version"
				instruction 'Private-Package', '*'
				// don't import any poi packages
				prependImport '!org.apache.poi.*', 'org.apache.xmlbeans.impl.schema'
			}
		}
	}
}

task wrapper(type: Wrapper) {
	gradleVersion = '2.2'
}
